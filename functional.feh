namespace F {
	function map (array a) {
		object f = recipient();
		if (f) {
			array result;
			a.each() using (i) {
				result.push(f.invoke(i));
			};
			return result;
		}
	}
	
	function fold (array a) {
		object f = recipient();
		if (f) {
			void result;
			a.each() using (i) {
				result = f.invoke(result,i);
			};
			return result;
		}
	}
	
	function fold_r (array a) {
		object f = recipient();
		if (f) {
			void result;
			number i;
			for (i = a.size()-1; i >= 0; i--) {
				result = f.invoke(result,a[i]);
			}
			return result;
		}
	}
	
	function zip (array a, array b) {
		object f = recipient();
		if (f) {
			array result;
			while (a.size() && b.size()) {
				result.push(f.invoke(a.shift(),b.shift()));
			}
			return result;
		}
	}
	
	function stride (array a,number stride_amount) {
		object f = recipient();
		if (f) {
			array result;
			array values;
			number i;
			while (a.size() > stride_amount) {
				values = [];
				for (i=0;i<stride_amount;i++) {
					values.push(a.shift());
				}
				result.push(f.invoke(values));
			}
			return result;
		}
	}
	
	function filter (array a) {
		object f = recipient();
		if (f) {
			array result;
			a.each() using (i) {
				if (f.invoke(i)) result.push(i);
			};
			return result;
		}
	}
}
